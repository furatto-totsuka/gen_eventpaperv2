CALL Modules\Excel
CALL Modules\RegExp
CONST MARK_CHARS = "◇◆■"
DIM excel
IF Excel.GETEXCEL() = NULL THEN
  MSGBOX("Excelを起動してください")
  EXIT
ENDIF
ws = Excel.CHOICESHEET(Excel.CHOICEFILE())
PUBLIC prevday
PUBLIC ym
PUBLIC err = ""
PUBLIC fileid
PUBLIC fileline
DIM h = SPLIT(ws.pageSetup.centerHeader)
DIM match = RegExp.EXECUTE(STRCONV(h[1], SC_HALFWIDTH), "(\d+)")
ym = match.item[0].value + "/" + match.item[1].value + "/"
// ヘッダ作成
fileid = FOPEN(GET_CUR_DIR + "\import.csv", F_WRITE1)
fileline = 1
FPUT(fileid, "Start Date", fileline, 1)
FPUT(fileid, "Start Time", fileline, 2)
FPUT(fileid, "End Date", fileline, 3)
FPUT(fileid, "End Time", fileline, 4)
FPUT(fileid, "Subject", fileline, 5)
FPUT(fileid, "Location", fileline, 6)
FPUT(fileid, "Description", fileline, 7)
fileline = 2
Excel.EACHROWS(ws.cells(1,1).currentRegion, "scanrows", TRUE)

IF err <> "" THEN
  MSGBOX("エラーを確認してください<#CR>" + err)
ELSE
  MSGBOX("完了")
ENDIF

FUNCTION scanrows(row, no)
  RESULT = TRUE
  IF row.cells(3).mergecells OR row.cells(5).value = "定休日" THEN
    EXIT
  ENDIF
  // 変数抜出し
  DIM day = row.cells(1).value
  DIM week = row.cells(2).value
  DIM time = row.cells(3).value
  DIM mark
  DIM name = row.cells(4).value
  DIM content = row.cells(5).value
  DIM ft
  DIM et
  // 値の設定
  mark = COPY(name, 1, 1)
  IF POS(MARK_CHARS, mark) > 0 THEN
    name = COPY(name, 2) 
  ELSE
    mark = ""
  ENDIF
  IF day = "" THEN
    day = prevday
  ELSE
    prevday = day
  ENDIF
  DIM times = SPLIT(STRCONV(time, SC_HALFWIDTH), "~")
  IF RESIZE(times) >= 1 THEN
    ft = times[0]
    et = times[1]
  ELSE
    ft = STRCONV(time, SC_HALFWIDTH)
    et = ""
    err = err + day + "日の" + mark + name + "の終了時間が未定<#CR>"
  ENDIF
  // 書き込み
  FPUT(fileid, ym + day, fileline, 1)
  FPUT(fileid, ft, fileline, 2)
  FPUT(fileid, ym + day, fileline, 3)
  FPUT(fileid, et, fileline, 4)
  FPUT(fileid, mark + name, fileline, 5)
  FPUT(fileid, "ふらっとステーション・とつか", fileline, 6)
  FPUT(fileid, content, fileline, 7)
  fileline = fileline + 1
FEND
